# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//drivers/adapter/uhdf2/uhdf.gni")

AUDIO_POLICY_SERVER_DIR =
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server"

ohos_prebuilt_etc("pulseaudio.rc") {
  source = "etc/pulseaudio.cfg"
  relative_install_dir = "init"
  part_name = "multimedia_audio_standard"
  subsystem_name = "multimedia"
}

config("audio_service_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "include/audio_service/server",
    "//foundation/multimedia/audio_standard/interfaces/inner_api/native/audiocommon/include",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//foundation/multimedia/audio_standard/frameworks/native/audiocapturer/include",
    "//foundation/multimedia/audio_standard/frameworks/native/audiorenderer/include",
    "//drivers/peripheral/audio/interfaces/include",
    "//drivers/peripheral/audio/hal/hdi_binder/proxy/include",
  ]

  if (target_cpu == "arm") {
    cflags = [ "-DBINDER_IPC_32BIT" ]
  }
}

ohos_shared_library("audio_service") {
  install_enable = true

  sources = [
    "src/audio_service/server/audio_manager_stub.cpp",
    "src/audio_service/server/audio_server.cpp",
  ]

  configs = [ ":audio_service_config" ]

  deps = [
    "//foundation/multimedia/audio_standard/frameworks/native/audiocapturer:audio_capturer_source",
    "//foundation/multimedia/audio_standard/frameworks/native/audiorenderer:audio_renderer_sink",
    "//foundation/multimedia/audio_standard/frameworks/native/pulseaudio/src/daemon:pulseaudio",
    "//foundation/multimedia/audio_standard/interfaces/inner_api/native/audiomanager:audio_client",
    "//utils/native/base:utils",
  ]

  external_deps = [
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr_standard:samgr_proxy",
  ]

  subsystem_name = "multimedia"
  part_name = "multimedia_audio_standard"
}

group("audio_policy_service_packages") {
  deps = [
    ":audio_dump",
    ":audio_interrupt_policy_config",
    ":audio_policy.rc",
    ":audio_policy_config",
    ":audio_policy_service",
  ]
}

ohos_prebuilt_etc("audio_policy.rc") {
  source = "etc/audio_policy.cfg"
  relative_install_dir = "init"
  part_name = "multimedia_audio_standard"
  subsystem_name = "multimedia"
}

config("audio_policy_public_config") {
  include_dirs = [
    "//drivers/peripheral/audio/interfaces/include",
    "//foundation/multimedia/audio_standard/services/include",
    "//foundation/multimedia/audio_standard/services/include/audio_service/client",
    "//foundation/distributedschedule/safwk/services/safwk/include",
    "//foundation/multimedia/audio_standard/frameworks/native/audioadapter/include",
    "//foundation/multimedia/audio_standard/frameworks/native/audiopolicy/include",
    "//foundation/multimedia/audio_standard/interfaces/inner_api/native/audiocommon/include",
    "//foundation/multimedia/audio_standard/interfaces/inner_api/native/audiomanager/include",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/common",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/config",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/interface",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/listener",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/manager",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include",
    "//foundation/multimedia/audio_standard/services/include/audio_policy/common",
    "//foundation/multimedia/audio_standard/services/include/audio_policy/server",
    "//foundation/multimedia/audio_standard/services/include/audio_policy/client",
    "//foundation/multimedia/audio_standard/services/include/audio_bluetooth/client",
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy/include",
    "//foundation/distributeddatamgr/distributeddatamgr/interfaces/innerkits/distributeddata/include",
    "//base/hiviewdfx/hilog/interfaces/native/inner_api/include",
    "//third_party/libxml2/include",
    "//third_party/pulseaudio/src",
    "//third_party/pulseaudio/confgure/src",
    "//third_party/pulseaudio/include",
    "//foundation/multimedia/audio_standard/frameworks/native/pulseaudio/include",
    "//utils/system/safwk/native/include",
    "//utils/native/base/include",
    "//third_party/bounds_checking_function/include",
    "//third_party/cJSON",
    "//drivers/peripheral/bluetooth/audio/interfaces/include",
    "//foundation/communication/bluetooth/services/bluetooth_standard/ipc/include",
    "//foundation/communication/bluetooth/services/bluetooth_standard/common/",
    "//foundation/communication/bluetooth/interfaces/innerkits/native_cpp/include",
    "//foundation/communication/bluetooth/interfaces/innerkits/native_cpp/framework/common",
    "//foundation/communication/bluetooth/interfaces/innerkits/native_c/include",
    "//foundation/multimedia/audio_standard/frameworks/native/audiorenderer/include",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]

  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  if ("${product_name}" == "m40") {
    cflags += [ "-DPRODUCT_M40" ]
  }

  ldflags = [ "-Wl" ]
}

ohos_shared_library("audio_dump") {
  install_enable = true
  sources = [ "//foundation/multimedia/audio_standard/services/src/audio_policy/server/audio_service_dump.cpp" ]

  public_configs = [ ":audio_policy_public_config" ]

  deps = [
    "//foundation/multimedia/audio_standard/frameworks/native/pulseaudio/src/pulse:pulse",
    "//third_party/bounds_checking_function:libsec_static",
    "//utils/native/base:utils",
  ]

  external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

  subsystem_name = "multimedia"
  part_name = "multimedia_audio_standard"
}

ohos_shared_library("audio_policy_service") {
  install_enable = true
  sources = [
    "//foundation/multimedia/audio_standard/services/src/audio_policy/client/audio_device_descriptor.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/audio_policy_manager_listener_proxy.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/audio_policy_manager_stub.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/audio_policy_server.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/audio_ringermode_update_listener_proxy.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/audio_volume_key_event_callback_proxy.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/audio_policy_service.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/audio_policy_service_proxy.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/config/audio_focus_parser.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/config/xml_parser.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/listener/device_status_listener.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/manager/audio_adapter_manager.cpp",
  ]

  public_configs = [ ":audio_policy_public_config" ]

  deps = [
    "$hdf_uhdf_path/hdi:libhdi",
    "$hdf_uhdf_path/host:libhdf_host",
    "$hdf_uhdf_path/ipc:libhdf_ipc_adapter",
    "$hdf_uhdf_path/utils:libhdf_utils",
    "//foundation/communication/bluetooth/services/bluetooth_standard/ipc:btipc_static",
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core:ipc_core",
    "//foundation/distributeddatamgr/distributeddatamgr/interfaces/innerkits/distributeddata:distributeddata_inner",
    "//foundation/distributeddatamgr/distributeddatamgr/services/distributeddataservice/adapter:distributeddata_adapter",
    "//foundation/multimedia/audio_standard/frameworks/native/audioadapter:pulse_audio_service_adapter",
    "//foundation/multimedia/audio_standard/frameworks/native/audiorenderer:audio_bluetooth_client",
    "//foundation/multimedia/audio_standard/services:audio_dump",
    "//foundation/multimodalinput/input/frameworks/proxy:libmmi-client",
    "//foundation/multimodalinput/input/frameworks/util:libmmi-util",
    "//third_party/libxml2:xml2",
    "//utils/native/base:utils",
  ]

  external_deps = [
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr_standard:samgr_proxy",
  ]
  subsystem_name = "multimedia"
  part_name = "multimedia_audio_standard"
}

ohos_prebuilt_etc("audio_policy_config") {
  if ("${product_name}" == "rk3566" || "${product_name}" == "rk3568") {
    source = "$AUDIO_POLICY_SERVER_DIR/etc/rk3568/audio_policy_config.xml"
  } else if ("${product_name}" == "m40") {
    source = "$AUDIO_POLICY_SERVER_DIR/etc/m40/audio_policy_config.xml"
  } else {
    source = "$AUDIO_POLICY_SERVER_DIR/etc/audio_policy_config.xml"
  }

  subsystem_name = "multimedia"
  module_install_dir = "etc/audio"
  part_name = "multimedia_audio_standard"
}

ohos_prebuilt_etc("audio_interrupt_policy_config") {
  source = "$AUDIO_POLICY_SERVER_DIR/etc/audio_interrupt_policy_config.xml"

  subsystem_name = "multimedia"
  module_install_dir = "etc/audio"
  part_name = "multimedia_audio_standard"
}
