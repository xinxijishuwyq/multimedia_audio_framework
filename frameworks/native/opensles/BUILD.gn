# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/audio_framework/config.gni")

pulseaudio_dir = "//third_party/pulseaudio"
opensles_dir = "//third_party/openSLES"

group("libsl") {
  public_deps = [ "//third_party/openSLES:libSLES" ]
}

config("audio_config") {
  include_dirs = [
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiorenderer/include",
    "//foundation/multimedia/audio_framework/frameworks/native/audiorenderer/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiocapturer/include",
    "//foundation/multimedia/audio_framework/frameworks/native/audiocapturer/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiocommon/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiostream/include",
    "//foundation/multimedia/audio_framework/interfaces/inner_api/native/audiomanager/include",
    "//foundation/multimedia/audio_framework/services/audio_service/client/include",
    "//commonlibrary/c_utils/base/include",
    "$pulseaudio_dir/src",
    "$pulseaudio_dir/confgure/src",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]
}

if (!audio_framework_feature_opensl_es) {
  group("opensles") {
    deps = []
  }
} else {
  opensles_version = "1.0.1"
  ohos_shared_library("opensles") {
    sanitize = {
      cfi = true
      debug = false
      blocklist = "${audio_fwk_path}/cfi_blocklist.txt"
    }
    include_dirs = [
      "$opensles_dir/api/$opensles_version",
      "${audio_fwk_path}/frameworks/native/opensles/include",
      "${audio_fwk_path}/interfaces/inner_api/native/andiorenderer/include/",
      "${audio_fwk_path}/interfaces/inner_api/native/andiocapturer/include/",
      "${audio_fwk_path}/interfaces/inner_api/native/andiomanager/include/",
    ]

    configs = [ ":audio_config" ]

    sources = [
      "$opensles_dir/api/$opensles_version/OpenSLES_IID.c",
      "$opensles_dir/api/$opensles_version/OpenSLES_OpenHarmony_IID.c",
      "${audio_fwk_path}/frameworks/native/opensles/src/adapter/audiocapturer_adapter.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/adapter/audioplayer_adapter.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/adapter/readorwritecallback_adapter.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/itf/engine_itf.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/itf/object_itf.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/itf/openharmony_buffer_queue_itf.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/itf/play_itf.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/itf/record_itf.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/itf/volume_itf.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/obj/audiocapturer_obj.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/obj/audioplayer_obj.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/obj/engine_obj.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/obj/output_mix_obj.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/util/builder.cpp",
      "${audio_fwk_path}/frameworks/native/opensles/src/util/table_struct.cpp",
    ]

    cflags = [ "-Werror" ]

    deps = [
      ":libsl",
      "${audio_fwk_path}/frameworks/native/audiocapturer:audio_capturer",
      "${audio_fwk_path}/frameworks/native/audiorenderer:audio_renderer",
      "${audio_fwk_path}/services/audio_service:audio_client",
    ]

    external_deps = [
      "hiviewdfx_hilog_native:libhilog",
      "ipc:ipc_single",
    ]

    output_name = "OpenSLES"
    output_extension = "so"
    subsystem_name = "multimedia"
    part_name = "audio_framework"
  }
}

group("audio_opensles_test_packages") {
  deps = [
    ":audio_opensles_player_test",
    ":audio_opensles_recorder_test",
  ]
}

config("audio_operation_config") {
  include_dirs = [
    "${audio_fwk_path}/interfaces/inner_api/native/audiorenderer/include",
    "${audio_fwk_path}/frameworks/native/audiorenderer/include",
    "${audio_fwk_path}/interfaces/inner_api/native/audiocapturer/include",
    "${audio_fwk_path}/frameworks/native/audiocapturer/include",
    "${audio_fwk_path}/interfaces/inner_api/native/audiocommon/include",
    "${audio_fwk_path}/interfaces/inner_api/native/audiostream/include",
    "${audio_fwk_path}/interfaces/inner_api/native/audiomanager/include",
    "${audio_fwk_path}/services/audio_service/client/include",
    "${audio_fwk_path}/services/audio_service/test/example",
    "//commonlibrary/c_utils/base/include",
    "$pulseaudio_dir/src",
    "$pulseaudio_dir/confgure/src",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]
}

config("audio_opensles_config") {
  include_dirs = [
    "${audio_fwk_path}/frameworks/native/opensles/include",
    "$opensles_dir/api/1.0.1",
  ]
}

if (!audio_framework_feature_opensl_es) {
  group("audio_opensles_player_test") {
    deps = []
  }
} else {
  ohos_executable("audio_opensles_player_test") {
    install_enable = false

    sources = [ "test/example/audio_opensles_player_test.cpp" ]

    configs = [
      ":audio_opensles_config",
      ":audio_operation_config",
    ]

    deps = [
      ":libsl",
      ":opensles",
    ]

    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

    part_name = "audio_framework"
    subsystem_name = "multimedia"
  }
}

if (!audio_framework_feature_opensl_es) {
  group("audio_opensles_recorder_test") {
    deps = []
  }
} else {
  ohos_executable("audio_opensles_recorder_test") {
    install_enable = false

    sources = [ "test/example/audio_opensles_recorder_test.cpp" ]

    configs = [
      ":audio_opensles_config",
      ":audio_operation_config",
    ]

    deps = [
      ":libsl",
      ":opensles",
    ]

    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]

    part_name = "audio_framework"
    subsystem_name = "multimedia"
  }
}
