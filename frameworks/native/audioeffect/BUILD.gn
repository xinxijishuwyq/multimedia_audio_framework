# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../audio_ohcore.gni")
import("../../../config.gni")
import("../../../sensor.gni")
import("../../../window_manager.gni")

pulseaudio_dir = "//third_party/pulseaudio"
pulseaudio_build_path = "//third_party/pulseaudio/ohosbuild"

config("audio_effect_config") {
  include_dirs = [
    "include",
    "../audiostream/include",
    "../../../interfaces/inner_api/native/audiorenderer/include",
    "../../../interfaces/inner_api/native/audiocommon/include",
    "../../../interfaces/inner_api/native/audiomanager/include",
    "../../../services/audio_service/client/include",
    "../../../services/audio_service/test/example",
    "$pulseaudio_dir/src",
    "$pulseaudio_dir/confgure/src",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]

  if (sensor_enable == true) {
    cflags += [ "-DSENSOR_ENABLE" ]
  }

  if (window_manager_enable == true) {
    cflags += [ "-DWINDOW_MANAGER_ENABLE" ]
  }
}

ohos_shared_library("audio_effect") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
    blocklist = "../../../cfi_blocklist.txt"
  }
  install_enable = true

  configs = [ ":audio_effect_config" ]

  # include_dirs = audio_gateway_include_dirs
  include_dirs = [ multimedia_audio_framework_pulse_audio ]

  sources = [
    "src/audio_effect_chain_manager.cpp",
    "src/audio_effect_hdi_param.cpp",
    "src/audio_effect_rotation.cpp",
    "src/audio_effect_volume.cpp",
    "src/audio_head_tracker.cpp",
  ]

  deps = [ "$pulseaudio_build_path/src/pulse:pulse" ]

  external_deps = [
    "c_utils:utils",
    "drivers_interface_audio:effect_idl_headers",
    "drivers_interface_audio:libeffect_proxy_1.0",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]

  if (sensor_enable == true) {
    external_deps += [ "sensor:sensor_interface_native" ]
  }

  if (window_manager_enable == true) {
    external_deps += [ "window_manager:libdm" ]
  }

  version_script = "../../../audio_framework.versionscript"

  part_name = "audio_framework"
  subsystem_name = "multimedia"
}
